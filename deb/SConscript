import os, shutil, sys
Import('env') # exported by parent SConstruct

# Here's the core info for the package

DEBNAME = "python-sdht-storage"
DEBVERSION = "0.5"
DEBMAINT = "Alexander Schussler [alex@xalx.net]"
DEBARCH = "i386"
DEBDEPENDS = "" # what are we dependent on?
DEBDESC = "A simpler kind of distributed hash table (DHT) with a stripped down version of the Chord algorithm"
REVISION = "001" # Lets just make somthing up at the moment (haven't set up my repository yet :)"

DEBFILES = [
    # Now we specify the files to be included in the .deb
    # Where they should go, and where they should be copied from.
    # If you have a lot of files, you may wish to generate this 
    # list in some other way.
    ("usr/lib/python2.5/site-packages/sdht.py", "#src/sdht.py"),
    ("usr/bin/minor-wsgi-storage",  "#src/minor-wsgi-storage.py"),
]

EXECUTABLES = [("usr/bin/sdht"),]
    
# This is the debian package we're going to create
debpkg = '#%s_%s-%s_%s.deb' % (DEBNAME, DEBVERSION, REVISION, DEBARCH)

# and we want it to be built when we build 'debian'
env.Alias("debian", debpkg)

DEBCONTROLFILE = os.path.join(DEBNAME, "DEBIAN/control")

# This copies the necessary files into place into place.
# Fortunately, SCons creates the necessary directories for us.
for f in DEBFILES:
    # We put things in a directory named after the package
    dest = os.path.join(DEBNAME, f[0])
    # The .deb package will depend on this file
    env.Depends(debpkg, dest)
    # Copy from the the source tree. Make it executable (should be oki
    # even for packages).
    env.Command(dest, f[1], [Copy('$TARGET','$SOURCE'), Chmod('$TARGET', 0755)])

    # The control file also depends on each source because we'd like
    # to know the total installed size of the package
    env.Depends(DEBCONTROLFILE, dest)

# Now to create the control file:
CONTROL_TEMPLATE = """
Package: %s
Priority: extra
Section: misc
Installed-Size: %s
Maintainer: %s
Architecture: %s
Version: %s-%s
Depends: %s
Description: %s

"""
env.Depends(debpkg, DEBCONTROLFILE)

# The control file should be updated when the SVN version changes
env.Depends(DEBCONTROLFILE, env.Value(REVISION))

# This function creates the control file from the template and info
# specified above, and works out the final size of the package.
def make_control(target=None, source=None, env=None):
    installed_size = 0
    for i in DEBFILES:
        installed_size += os.stat(str(env.File(i[1])))[6]
    control_info = CONTROL_TEMPLATE % (
        DEBNAME, installed_size, DEBMAINT, DEBARCH, DEBVERSION,
        REVISION, DEBDEPENDS, DEBDESC)
    f = open(str(target[0]), 'w')
    f.write(control_info)
    f.close()
    
# We can generate the control file by calling make_control
env.Command(DEBCONTROLFILE, None, make_control)

# And we can generate the .deb file by calling dpkg-deb
env.Command(debpkg, DEBCONTROLFILE,
            "dpkg-deb -b %s %s" % ("deb/%s" % DEBNAME, "$TARGET"))

